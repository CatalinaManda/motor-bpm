buildscript {
    ext {
        release_group = "com.ledgertech.motor"
        kotlin_version = "1.3.41"
        junit_version = "4.12"
        slf4j_version = "1.7.25"
        log4j_version = "2.9.1"
        spring_boot_version = "2.2.2.RELEASE"

        ktor_version = '1.2.6'

        corda_group = "net.corda"
        corda_version = "4.3"
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.2.4.RELEASE"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.41"
    }
}

plugins {
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "org.jetbrains.kotlin.jvm" version '1.3.41'
    id "org.jetbrains.kotlin.plugin.spring" version '1.3.41'
    id "org.jetbrains.kotlin.plugin.noarg" version '1.3.41'
    id "org.jetbrains.kotlin.plugin.jpa" version '1.3.41'
    id "org.jetbrains.kotlin.plugin.allopen" version '1.3.41'
}

version = "0.0.1-SNAPSHOT"
group = "com.ledgertech"

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

allprojects {
    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

subprojects {
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "org.jetbrains.kotlin.plugin.spring"
    apply plugin: "org.jetbrains.kotlin.plugin.noarg"
    apply plugin: "org.jetbrains.kotlin.plugin.jpa"
    apply plugin: "org.jetbrains.kotlin.plugin.allopen"
    apply plugin: 'maven-publish'

    version = parent.version
    group = parent.group

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()

        maven {
            url 'https://nexus.ledgertech.com/repository/lt-maven-public/'
            credentials {
                username project.'repos.maven2.ledgertech.username'
                password project.'repos.maven2.ledgertech.password'
            }
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "org.jetbrains.kotlin:kotlin-reflect"

        testImplementation "org.jetbrains.kotlin:kotlin-test"
        testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            languageVersion = "1.2"
            apiVersion = "1.2"
            jvmTarget = "1.8"
            javaParameters = true
        }
    }

    publishing {
        repositories {
            maven {
                def releasesRepoUrl = 'https://nexus.ledgertech.com/repository/lt-maven-release/'
                def snapshotsRepoUrl = 'https://nexus.ledgertech.com/repository/lt-maven-snapshot/'

                url = project.getVersion().endsWith('-SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username project.'repos.maven2.ledgertech.username'
                    password project.'repos.maven2.ledgertech.password'
                }
            }
        }

        publications {
            maven(MavenPublication) {
                artifactId = project.name
                from components.java
            }
        }
    }
}
